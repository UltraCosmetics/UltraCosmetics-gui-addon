import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    // https://github.com/GradleUp/shadow
    id 'com.gradleup.shadow' version '8.3.9'
    // https://github.com/Minecrell/plugin-yml
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
    id 'idea'
}

group 'me.datatags.ultracosmetics'
version '1.4'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    mavenCentral()

    // SpigotMC
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    // Dependencies
    maven { url = 'https://repo.codemc.io/repository/maven-snapshots/' }
    // UltraCosmetics
    maven { url = 'https://jitpack.io/' }
}

dependencies {
    // https://github.com/google/gson
    library 'com.google.code.gson:gson:2.13.1'
    // https://hub.spigotmc.org/stash/projects/SPIGOT/repos/spigot/browse
    compileOnly 'org.spigotmc:spigot-api:1.21.8-R0.1-SNAPSHOT'
    // https://github.com/UltraCosmetics/UltraCosmetics
    compileOnly ('com.github.UltraCosmetics:UltraCosmetics:3.13.1-RELEASE') {
        attributes {
            // Depend on the version with dependencies shaded so we don't need to shade them separately
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.SHADOWED))
        }
    }
    // https://github.com/Bastian/bstats-metrics
    implementation 'org.bstats:bstats-bukkit:3.1.0'
}

shadowJar {
    minimize()
    def path = 'me.datatags.ultracosmeticsguiaddon.shaded'
    relocate('org.bstats', path + '.bstats')
}

bukkit {
    name = 'UltraCosmeticsGUIAddon'
    authors = ['datatags', 'dennishzg']
    main = 'me.datatags.ultracosmeticsguiaddon.UltraCosmeticsGUIAddon'
    apiVersion = '1.17'
    depend = ['UltraCosmetics']
}

processResources {
    inputs.property("version", project.version)

    filter ReplaceTokens, tokens: [
            'VERSION'   : project.version,
    ]
}
